# ---
# Module: Mobile NixOS Base
# Description: Mobile NixOS specific hacks and stage-1 settings
# Scope: System
# ---

{ config, lib, pkgs, stage2Only ? false, ... }:

let
  headlessStage1Source = pkgs.writeText "sheng-headless-stage1.rb" (
    (builtins.readFile ../patches/stage-1-headless-no-gui.rb)
    + "\n"
    + (builtins.readFile ../patches/stage-1-early-charge-guard.rb)
    + "\n"
    + (builtins.readFile ../patches/stage-1-headless-generation-menu.rb)
    + "\n"
    + (builtins.readFile ../patches/stage-1-boot-animation.rb)
  );
  headlessStage1Task = pkgs.runCommand "sheng-headless-stage1-task" { } ''
    mkdir -p $out
    cat ${pkgs.sheng-fb-painter}/share/sheng/menu-font.rb \
      ${headlessStage1Source} > $out/zz-sheng-headless-stage1.rb
  '';
  udevTolerantTask = pkgs.writeTextDir "zz-sheng-udev-tolerant.rb" (
    builtins.readFile ../patches/stage-1-udev-trigger-tolerant.rb
  );
  rootFsckTask = pkgs.writeTextDir "zz-sheng-root-fsck.rb" (
    builtins.readFile ../patches/stage-1-root-fsck.rb
  );
  stage1Firmware = pkgs.runCommand "sheng-stage1-firmware" { } ''
    mkdir -p $out/lib/firmware
    cp -r ${pkgs.sheng-firmware}/lib/firmware/qcom $out/lib/firmware/
  '';
  rootfsFirmware = pkgs.buildEnv {
    name = "sheng-rootfs-firmware";
    paths = [
      pkgs.sheng-firmware
      pkgs.sheng-touch-firmware
      pkgs.wireless-regdb
    ];
    pathsToLink = [ "/lib/firmware" ];
    ignoreCollisions = false;
  };
  closureInfo = pkgs.buildPackages.closureInfo {
    rootPaths = config.system.build.toplevel;
  };
  kernelModulesTree = pkgs.runCommand "sheng-kernel-modules-tree" {
    nativeBuildInputs = [
      pkgs.buildPackages.kmod
    ];
  } ''
    mkdir -p $out/lib
    cp -r ${config.mobile.boot.stage-1.kernel.package}/lib/modules $out/lib/
    chmod -R u+w $out/lib/modules

    version="$(basename "$out"/lib/modules/*)"
    depmod -b "$out" "$version"
  '';
  udevadmWrapper = pkgs.writeShellScript "udevadm-trigger-wrapper" ''
    out=$(${config.systemd.package}/bin/udevadm trigger "$@" 2>&1)
    ret=$?
    
    if [ $ret -ne 0 ]; then
      filtered=$(echo "$out" | grep -v 'qcom-battmgr' | grep -v 'Resource temporarily unavailable' || true)
      if [ -n "$filtered" ]; then
        echo "$filtered" >&2
        exit $ret
      fi
      exit 0
    fi
  '';
in
{
  mobile.enable = true;

  mobile.generatedFilesystems.rootfs = lib.mkForce {
    name = "nixos-sheng-rootfs";
    filesystem = "ext4";
    label = "linux";
    ext4.partitionID = "ee8d3593-59b1-480e-a3b6-4fefb17ee7d8";
    location = "/rootfs.img";
    extraPadding = 1024 * 1024 * 1024;

    # Mobile NixOS defaults to Android's legacy make_ext4fs. Use current
    # e2fsprogs so new images carry checksums for directories, inodes, block
    # bitmaps, and the journal instead of discovering damage only on access.
    #
    # The image is populated by an unprivileged build user, so `cp -prf`
    # cannot preserve root ownership and every file copied into the rootfs
    # would end up owned by the build uid. NetworkManager refuses to load
    # device plugins that are not owned by root ("file has invalid owner
    # (should be root)"), which silently disables Wi-Fi inside NM: `nmcli
    # device wifi` reports "No Wi-Fi device found" and nmtui lists no
    # networks, while `iw scan` keeps working. nixpkgs' own ext4 image
    # builder wraps its populate and mkfs steps in fakeroot for the same
    # reason. Record root ownership in a single fakeroot session so
    # `mkfs.ext4 -d` reads uid/gid 0.
    # `nativeBuildInputs` cannot be extended from here without dropping the
    # `e2fsprogs`/`make_ext4fs` entries that ext4.nix adds, so reference
    # fakeroot by absolute store path; that still records the build dependency.
    buildPhases.copyPhase = lib.mkForce ''
      faketime -f "1970-01-01 00:00:01" \
        ${pkgs.buildPackages.fakeroot}/bin/fakeroot -- bash -c "
          set -eu
          chown -R 0:0 .
          mkfs.ext4 \
            -F \
            -b $blockSize \
            -e remount-ro \
            -m 0 \
            -O metadata_csum,64bit,dir_index,extent,flex_bg,huge_file,extra_isize,dir_nlink \
            -E lazy_itable_init=0,lazy_journal_init=0 \
            -L linux \
            -U ee8d3593-59b1-480e-a3b6-4fefb17ee7d8 \
            -d . \
            $img
        "
    '';

    # Keep this aligned with Mobile NixOS' default rootfs.nix populate logic.
    populateCommands = ''
      mkdir -p ./nix/store
      echo "Copying system closure..."

      err=0
      while IFS= read -r path; do
        echo "  Copying $path"
        if test -e "$path"; then
          cp -prf "$path" ./nix/store
        else
          2>&1 printf "ERROR: path %q does not exist...\n" "$path"
          (( ++err ))
        fi
      done < "${closureInfo}/store-paths"

      if (( err > 0 )); then
        2>&1 printf "... Bailing out, %d errors.\n" "$err"
        exit 2
      fi

      echo "Done copying system closure..."
      cp -v ${closureInfo}/registration ./nix-path-registration

      echo "Creating system profile..."
      mkdir -p ./nix/var/nix/profiles
      ln -s ${config.system.build.toplevel} ./nix/var/nix/profiles/system-1-link
      ln -s system-1-link ./nix/var/nix/profiles/system

      echo "Injecting sheng rootfs firmware into /lib/firmware..."
      mkdir -p ./lib/firmware
      # Mobile NixOS' custom rootfs population does not copy the NixOS
      # firmware aggregate automatically. Merge the device-specific packages
      # first, then materialize them once so read-only store directories cannot
      # block a later package from adding files to the same subtree.
      cp -rL ${rootfsFirmware}/lib/firmware/. ./lib/firmware/

      echo "Injecting kernel modules into /lib/modules..."
      if [ -d ${kernelModulesTree}/lib/modules ]; then
        mkdir -p ./lib/modules
        cp -r ${kernelModulesTree}/lib/modules/* ./lib/modules/
      else
        echo "WARNING: sheng kernel modules tree has no lib/modules directory"
      fi
    '';

    additionalCommands = ''
      echo ":: Adding hydra-build-products"
      (PS4=" $ "; set -x
      mkdir -p $out_path/nix-support
      cat <<EOF > $out_path/nix-support/hydra-build-products
      file rootfs $img
      EOF
      )
    '';
  };

  fileSystems."/" = lib.mkForce {
    device = "/dev/disk/by-partlabel/linux";
    fsType = "ext4";
    neededForBoot = true;
    autoResize = true;
    options = [
      "noatime"
      "data=ordered"
      "barrier=1"
      "errors=remount-ro"
    ];
  };

  # Mobile NixOS stage-1 already checks the offline filesystem and expands it
  # to the existing linux partition. Do not run cloud-image partition growth
  # or a second online ext4 resize after switch-root on this Android GPT.
  boot.growPartition = lib.mkForce false;
  systemd.units."systemd-growfs-root.service".enable = false;

  mobile.boot.stage-1 = {
    compression = "gzip";
    crashToBootloader = false;

    bootConfig = {
      # Keep normal boot output concise while retaining filesystem progress,
      # warnings, and errors. Kernel and systemd logs remain available later.
      log.level = "INFO";
      boot.fail.shell = true;
      gui.enable = false;
      splash.disabled = true;
      sheng_boot_animation.enable = true;
      sheng_generation_menu = {
        enable = true;
        timeout = 3;
      };
      sheng_early_charge_guard = {
        enable = true;
        critical_capacity = 2;
        boot_capacity = 5;
        # Charger-mode boots hand off to the low-power userspace target. This
        # timeout only guards an explicitly requested normal boot.
        max_wait_seconds = 30;
      };
    };

    gui.enable = false;

    tasks = [
      headlessStage1Task
      rootFsckTask
      udevTolerantTask
    ];

    contents = [
      { object = pkgs.sheng-boot-animation; symlink = "/etc/sheng-boot-animation"; }
    ];

    extraUtils = [
      pkgs.kbd
      pkgs.sheng-fb-painter
    ];

    shell.shellOnFail = true;

    kernel.modules = [ ];
    kernel.additionalModules = [ ];
    # Keep large device firmware in rootfs. Stage-1 only needs Qualcomm boot
    # firmware; including the full package makes boot.img exceed boot_b.
    firmware = [ stage1Firmware ];
  };

  mobile.boot.stage-1.fail.reboot = false;

  mobile.adbd.enable = lib.mkDefault true;

  mobile.beautification.silentBoot = lib.mkForce false;

  boot.kernel.enable = lib.mkIf stage2Only (lib.mkForce false);
  # 新 nixpkgs 里 bootspec 总是生成、无法再关闭（旧写法会触发断言），
  # 这里不再覆盖 boot.bootspec.enable。
  hardware.deviceTree.enable = lib.mkIf stage2Only (lib.mkForce false);
  system.modulesTree = lib.mkForce (
    lib.optional (!stage2Only) kernelModulesTree
  );
  system.systemBuilderCommands = lib.mkIf stage2Only (lib.mkAfter ''
    # Stage-2 rebuilds reuse the kernel and modules already installed by the
    # flashable rootfs instead of adding the kernel build to their closure.
    ln -sfn /lib/modules "$out/kernel-modules"
    ln -sfn ${config.hardware.firmware}/lib/firmware "$out/firmware"
  '');
  environment.etc = lib.mkIf stage2Only {
    "modules-load.d/sheng-stage2.conf".text =
      lib.concatStringsSep "\n" config.boot.kernelModules + "\n";
  };

  documentation.enable = false;

  # Wrap udevadm trigger in stage-2 to prevent qcom-battmgr from polluting the journal with fatal errors
  systemd.services.systemd-udev-trigger.serviceConfig.ExecStart = lib.mkForce [
    ""
    "${udevadmWrapper} --type=subsystems --action=add"
    "${udevadmWrapper} --type=devices --action=add"
  ];
}
